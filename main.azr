//プロトコル制御用
dict dictOptionalHeader;
//セーブ用
dict dictSaveData;
load()
{
	_script_load("OnMenuExec");
	_script_load("util");

	dictOptionalHeader += ${
		$("target", ""),
		$("event", ""),
		$("event_option", ""),
		$("script_option", ""),
		$("reference", {}),
		$("marker", "")
	};

	dictSaveData = _vload("savedata");
	if (dictSaveData["officeId"] == nil) {
		dictSaveData += ${
			$("officeId", "")
		};
	}
}

unload()
{
	_vsave("savedata", dictSaveData);
}

array _customrequest(dict ref)
{
	array ar;
	// PLUGINに対するリクエストでない場合
	if (_strstr(ref["head"], "PLUGIN") == -1) {
		ar	+=	"PLUGIN/2.0 400 Bad Request";
		ar	+=	"Sender: akari";
		ar	+=	"Charset: UTF-8";
		ar	+=	"";
		ar	+=	"";
		return ar;
	}
	// セキュリティレベル="External"なら即返る
	if (ref["SecurityLevel"] == "External") {
		ar	+=	"PLUGIN/2.0 204 No Content";
		ar	+=	"Sender: akari";
		ar	+=	"Charset: UTF-8";
		ar	+=	"";
		ar	+=	"";
		return ar;
	}
	string s = _eventdict[ref["ID"]];
	if (s == "") s = _fncstr(ref["ID"], ref);
	// NOTIFY
	if (_strstr(ref["head"], "NOTIFY") != -1) {
		resetOptionalHeader();
		ar	+=	"PLUGIN/2.0 200 OK";
		ar	+=	"Sender: akari";
		ar	+=	"Charset: UTF-8";
		ar	+=	"";
		ar	+=	"";
		return ar;
	}
	// GET
	if (_strstr(ref["head"], "GET") != -1) {
		ar	+=	"PLUGIN/2.0 200 OK";
		ar	+=	"Sender: akari";
		ar	+=	"Charset: UTF-8";
		if (s != "") {
			ar	+=	"Script: " + s;
			makeDefaultEventAndReference(ref, s);
		}
		dict d = dictOptionalHeader;
		if (d["target"] != "") {
			ar	+=	"Target: " + d["target"];
		}
		if (d["event"] != "") {
			ar	+=	"Event: " + d["event"];
		}
		if (d["event_option"] != "") {
			ar	+=	"EventOption: " + d["event_option"];
		}
		if (d["script_option"] != "") {
			ar	+=	"ScriptOption: " + d["script_option"];
		}
		if (_aryvn(d["reference"]) > 0) {
			for(int i = 0; d["reference"][i] != nil; i++) {
				ar	+=	"Reference" + i + ": " + d["reference"][i];
			}
		}
		if (d["marker"] != "") {
			ar	+=	"Marker: " + d["marker"];
		}
		resetOptionalHeader();
		ar	+=	"";
		ar	+=	"";
		return ar;
	}
	// 未知のリクエスト
	else {
		ar	+=	"PLUGIN/2.0 400 Bad Request";
		ar	+=	"Sender: akari";
		ar	+=	"Charset: UTF-8";
		ar	+=	"";
		ar	+=	"";
		return ar;
	}
}

makeDefaultEventAndReference(dict ref, string script)
{
	dictOptionalHeader["event"] = "OnPluginExec";
	array a = {
		"Version=" + version(),
		"From=" + pluginname(), 
		"Script=" + script,
		"Event=" + ref["ID"]
	};
	for (int i = 0; ref["Reference" + i] != nil; i++) {
		a += ref["Reference" + i];
	}
	dictOptionalHeader["reference"] = a;
}

resetOptionalHeader()
{
	dictOptionalHeader["target"] = "";
	dictOptionalHeader["event"] = "";
	dictOptionalHeader["event_option"] = "";
	dictOptionalHeader["script_option"] = "";
	dictOptionalHeader["reference"] = {};
	dictOptionalHeader["marker"] = "";
}
